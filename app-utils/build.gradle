apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'com.spiddekauga.android'
version = '0.1.0-SNAPSHOT'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

configurations {
    deployerJars
    provided
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(':common-utils')
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile "com.squareup.okhttp3:okhttp-urlconnection:3.2.0"
    compile 'com.github.franmontiel:PersistentCookieJar:v0.9.3'
    compile 'com.google.code.gson:gson:2.7'
    compile project(path: ':cloudshine', configuration: 'android-endpoints')
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

//install {
//    repositories.mavenInstaller {
//        pom.artifactId = project.name
//    }
//}

uploadArchives {
    repositories {
        // Set mavenRepositoryUrl & mavenUserName inside gradle.properties
        mavenDeployer {
            configuration = configurations.deployerJars
            pom.artifactId = project.name
            repository(url: mavenRepositoryUrl + '/releases') {
                authentication(userName: mavenUserName)
            }
            snapshotRepository(url: mavenRepositoryUrl + '/snapshots') {
                authentication(userName: mavenUserName)
            }
        }
    }
}
